opengt
set com 115200n81
set senddelay 0.05
waitquiet 1 0.2
system "nvram set g3progress=0"
send "AT+CPIN?^m"
waitfor 30 "SIM PUK","SIM PIN","READY","ERROR","ERR"
if % = -1 goto simerror
if % = 0 goto ready
if % = 1 goto getpin
if % = 2 goto ready
if % = 3 goto error
if % = 4 goto error
:simerror
system "nvram set simerr=1"
system "nvram set g3err=1"
print $s," ***SIM ERROR***\n"
print "Check device port configuration.Check SIM is inserted\nTest SIM in a mobile phone?\n"
send "ATH^m"
exit 1
:getpin
system "nvram set g3state_pin=1"
let $x=$rpipe("nvram get modem_pincode")
let a=len($x)
if a=0 goto pinenverr
if a<>4 goto pinerror
let c=0
:test
let $c=$mid($x,c,1)
if $c<"0" goto pinerror
if $c>"9" goto pinerror
inc c
if c<4 goto test
let a=val($x)
if a<0 goto pinerror
if a>9999 goto pinerror
let $c=$left($x,4)
let $y=$rpipe("nvram get pinerr")
let a=val($y)
if a=1 goto pinerror
#if a=2 goto pintimeerror
if a=3 goto pinenverr
:enterpin
system "nvram set g3state_pin=2"
send "AT+CPIN=\""
send $c
send "\"^m"
waitfor 20 "OK","ERR","ERROR","+CME ERROR"
if % = -1 goto pintimeerror
if % = 0 goto ready
:pinerror
system "nvram set pinerr=1"
system "nvram set g3err=1"
print "ERROR: PIN code must be 4 decimal digits only\n"
print "Caution! - entering the wrong PIN code three times will lock the SIM\n"
send "ATH^m"
exit 1
:pinenverr
system "nvram set pinerr=3"
system "nvram set g3err=1"
print "ERROR: The COMGTPIN env variable is not set\n"
send "ATH^m"
exit 1
:pintimeerror
system "nvram set pinerr=2"
system "nvram set g3err=1"
print "ERROR: timeout, device did not respond to PIN command entry.\n"
send "ATH^m"
exit 1
:ready
system "nvram set g3state_rd=1"
system "nvram set simerr=0"
system "nvram set connerr=0"
system "nvram set pinerr=0"
system "nvram set apnerr=0"
system "nvram set g3err=0"
system "nvram set g3progress=1"
send "AT^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto next10
goto error
:next10
print "PASS 1\n"
system "nvram set g3progress=2"
send "AT+CFUN=1^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto next20
goto error
:next20
print " PASS 2\n"
system "nvram set g3progress=3"
send "ATS0=0^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto next30
goto error
:next30
print "PASS 3\n"
system "nvram set g3progress=4"
let $x=$rpipe("nvram get modem_apn")
let a=len($x)
if a=0 goto apnerror
if a>32 goto apnerror
send "AT+CGDCONT=1,\"IP\",\""
send $x
send "\"^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto apntimeerror
if % = 0 goto next2
goto apnerror
:next2
print " PASS 4\n"
system "nvram set g3progress=5"
let $x=$rpipe("nvram get modem_dialnum")
let a=len($x)
if a=0 goto apnerror
send "ATD"
send $x
send "^m"
waitfor 10 "CONNECT","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto next3
goto error
:next3
system "nvram set g3progress=5"
print "CONNECTED\n"
exit 0
:apnerror
system "nvram set apnerr=1"
system "nvram set g3err=1"
print "ERROR entering APN\n"
print \"The COMGTAPN env variable is not set\n"
send "ATH^m"
exit 1
:apntimeerror
system "nvram set apnerr=2"
system "nvram set g3err=1"
print \"ERROR entering APN\n"
print "The device timeout.\n"
send "ATH^m"
exit 1
:error
system "nvram set connerr=1"
system "nvram set g3err=1"
print "CONNECT ERROR\n"
send "ATH^m"
exit 1
:timeerror
system "nvram set connerr=2"
system "nvram set g3err=1"
print "CONNECT TIMEOUT\n"
send "ATH^m"
exit 1

