opengt
set com 115200n81
set senddelay 0.05
waitquiet 1 0.2

system "nvram set g3state_pin=0"
system "nvram set g3state_z=0"
system "nvram set g3state_q0=0"
system "nvram set g3state_cd=0"
system "nvram set g3state_class=0"
system "nvram set g3state_mode=0"
system "nvram set g3state_apn=0"
system "nvram set g3state_dial=0"
system "nvram set g3state_conn=0"

system "nvram set g3state_pin=1"
let $x=$rpipe("nvram get modem_pincode")
let l=len($x)
if l=0 goto ready
if l<>4 goto pinerror


:test
let c=0
let $c=$mid($x,c,1)
if $c<"0" goto pinerror
if $c>"9" goto pinerror
inc c
if c<4 goto test
let a=val($x)
if a<0 goto pinerror
if a>9999 goto pinerror
let $c=$left($x,4)
let $y=$rpipe("nvram get g3err_pin")
let a=val($y)
if a=1 goto pinerror
if a=3 goto pinerror2

send "AT+CPIN?^m"
waitfor 20 "SIM PIN","READY","ERROR","ERR"
if % = -1 goto pintimeerror
if % = 0 goto enterpin
if % = 1 goto ready
goto pinerror


:enterpin
system "nvram set g3state_pin=2"
send "AT+CPIN=\""
send $c
send "\"^m"
waitfor 20 "OK","ERR","ERROR","+CME ERROR"
if % = -1 goto pintimeerror
if % = 0 goto ready
#system "nvram set g3err_pin=3"
#goto pinerror2


:ready
system "nvram set g3state_z=1"
send "ATZ^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto set_parameters
system "nvram set g3state_z=-1"
goto error


:set_parameters
system "nvram set g3state_q0=1"
send "ATQ0 V1 E1 &C1 &D2 S0=0^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto set_parameters2
system "nvram set g3state_q0=-1"
goto error


:set_parameters2
system "nvram set g3state_cd=1"
send "AT&C1 &D2^m"
waitfor 10 "OK","ERR","ERROR","COMMAND NOT SUPPORT"
if % = -1 goto timeerror
if % = 0 goto disable_fax
system "nvram set g3state_cd=-1"


:disable_fax
system "nvram set g3state_class=1"
send "AT+FCLASS=0^m"
waitfor 10 "OK","ERR","ERROR","COMMAND NOT SUPPORT"
if % = -1 goto timeerror
if % = 0 goto set_mode
system "nvram set g3state_class=-1"


:set_mode
system "nvram set g3state_mode=1"
let $m=$rpipe("nvram get modem_mode")
let l=len($m)
if l=0 let m1=0
if l!=0 let m1=val($m)
let $v=$rpipe("nvram get modem_vid")
let l=len($v)
if l=0 goto set_apn
let b=val($v)
if b=0x12d1 goto mode_huawei_ex
if b=0x19d2 goto mode_zte
if b=0x1199 goto mode_sierra
goto set_apn


:mode_huawei_ex
system "nvram set g3state_mode=2.1"
send "AT\^SYSCFGEX?^m"
waitfor 10 "OK","ERR","ERROR","COMMAND NOT SUPPORT"
if % = -1 goto timeerror
if % < -1 goto mode_huawei
if % > 0 goto mode_huawei

send "AT\^SYSCFGEX=\""
if m1=2 send "01"
if m1=3 send "02"
if m1=4 send "03"
if m1=43 send "0302"
if m1=0 send "00"
send "\",3FFFFFFF,1,2,7FFFFFFFFFFFFFFF,,^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto set_apn
system "nvram set g3state_mode=-2.1"
goto error


:mode_huawei
system "nvram set g3state_mode=2.2"
send "AT\^SYSCFG?^m"
waitfor 10 "OK","ERR","ERROR","COMMAND NOT SUPPORT"
if % = -1 goto timeerror
if % < -1 goto error
if % > 0 goto error

if m1=4 goto error
send "AT\^SYSCFG="
if m1=2 send "13"
if m1=3 send "14"
if m1=4 goto error
if m1=43 send "14"
if m1=0 send "2"
send ",0,3FFFFFFF,1,2^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto set_apn
system "nvram set g3state_mode=-2.2"
goto error


:mode_zte
system "nvram set g3state_mode=3"
send "AT+ZSNT?^m"
waitfor 10 "OK","ERR","ERROR","COMMAND NOT SUPPORT"
if % = -1 goto timeerror
if % < -1 goto error
if % > 0 goto error

send "AT+ZSNT="
if m1=2 send "1"
if m1=3 send "2"
if m1=4 send "6"
if m1=43 send "0"
if m1=0 send "0"
send ",0,"
if m1=2 send "0"
if m1=3 send "0"
if m1=4 send "0"
if m1=43 send "6"
if m1=0 send "0"
send "^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto set_apn
system "nvram set g3state_mode=-3"
goto error


:mode_sierra
system "nvram set g3state_mode=4"
send "AT!SELRAT?^m"
waitfor 10 "OK","ERR","ERROR","COMMAND NOT SUPPORT"
if % = -1 goto timeerror
if % < -1 goto error
if % > 0 goto error

send "AT!SELRAT="
if m1=2 send "02"
if m1=3 send "01"
if m1=4 send "06"
if m1=43 send "07"
if m1=0 send "00"
send "^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto set_apn
system "nvram set g3state_mode=-4"
goto error


:set_apn
system "nvram set g3state_apn=1"
let $x=$rpipe("nvram get modem_apn")
let l=len($x)
if l=0 goto apnerror
if l>32 goto apnerror
send "AT+CGDCONT=1,\"IP\",\""
send $x
send "\"^m"
waitfor 10 "OK","ERR","ERROR"
if % = -1 goto apntimeerror
if % = 0 goto set_dial


:set_dial
system "nvram set g3state_dial=1"
let $x=$rpipe("nvram get modem_dialnum")
let l=len($x)
if l=0 goto error
send "ATD"
send $x
send "^m"
waitfor 10 "CONNECT","ERR","ERROR"
if % = -1 goto timeerror
if % = 0 goto done
system "nvram set g3state_dial=-1"
goto error


:done
system "nvram set g3state_conn=1"
print "CONNECTED\n"
exit 0


:pinerror
system "nvram set g3err_pin=1"
print "ERROR: PIN code must be 4 decimal digits only.\n"
print "Caution! - entering the wrong PIN code three times will lock the SIM.\n"
send "ATH^m"
exit 1


:pinerror2
system "nvram set g3err_pin=3"
print "ERROR: PIN code is incorrect at present.\n"
print "Caution! - entering the wrong PIN code three times will lock the SIM.\n"
send "ATH^m"
exit 1


:pintimeerror
system "nvram set g3err_pin=2"
print "ERROR: timeout, device did not respond to PIN command entry.\n"
send "ATH^m"
exit 1


:apnerror
system "nvram set g3err_apn=1"
print "ERROR entering APN.\n"
print \"The COMGTAPN env variable is not set.\n"
send "ATH^m"
exit 1


:apntimeerror
system "nvram set g3err_apn=2"
print \"ERROR entering APN.\n"
print "The device timeout.\n"
send "ATH^m"
exit 1


:error
system "nvram set g3err_conn=1"
print "CONNECT ERROR.\n"
send "ATH^m"
exit 1


:timeerror
system "nvram set g3err_conn=2"
print "CONNECT TIMEOUT.\n"
send "ATH^m"
exit 1
